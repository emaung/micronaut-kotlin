plugins {
    id "org.gradle.maven-publish"
    id("micronaut-kotlin.module-conventions")
    id "com.google.cloud.artifactregistry.gradle-plugin" version "2.1.5"
}

version = "3.2.1"

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    maven {
        url "artifactregistry://australia-southeast1-maven.pkg.dev/foundation-268322/maven"
    }
    maven {
        url "artifactregistry://australia-southeast1-maven.pkg.dev/foundation-268322/maven-release"
    }
}

sourceCompatibility = 1.8
compileKotlin { kotlinOptions.jvmTarget = "1.8" }
compileTestKotlin { kotlinOptions.jvmTarget = "1.8" }

dependencies {
    kapt platform("io.micronaut:micronaut-bom:$micronautVersion")
    kapt "io.micronaut:micronaut-inject-java"

    annotationProcessor "io.micronaut:micronaut-inject-java"

    compileOnly "org.graalvm.nativeimage:svm"

    api "io.micronaut:micronaut-inject"

    implementation "com.fasterxml.jackson.module:jackson-module-kotlin", {
        exclude group:"org.jetbrains.kotlin", module:'kotlin-reflect'
    }
    implementation 'com.typesafe:config:1.4.2'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    testRuntimeOnly "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    testImplementation "io.micronaut.test:micronaut-test-junit5"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testImplementation "io.micronaut:micronaut-runtime"

    testRuntimeOnly "ch.qos.logback:logback-classic:1.2.11"
}

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            def snapshotURL = uri("artifactregistry://australia-southeast1-maven.pkg.dev/foundation-268322/maven")
            def releaseURL = uri("artifactregistry://australia-southeast1-maven.pkg.dev/foundation-268322/maven-release")
            if (project.version.toString().endsWith("SNAPSHOT")) {
                url = snapshotURL
            } else {
                url = releaseURL
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = "com.remlyhq"
            artifactId = "micronaut-kotlin-runtime"
            from components.java
        }
    }
}

